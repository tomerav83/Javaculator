plugins {
    id 'java'
    id 'application'
    id 'jacoco'
    id 'antlr'
}

group = 'org.javaculator'

repositories {
    mavenCentral()
}

dependencies {
    antlr "org.antlr:antlr4:4.13.1"
    implementation "org.antlr:antlr4-runtime:4.13.1"
    implementation 'org.jline:jline:3.25.0'
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
}

generateGrammarSource {
    maxHeapSize = "64m"
    arguments += ["-visitor", "-long-messages"]
}

//sourceSets.main.java.srcDirs += 'build/generated-src/antlr/main'

compileJava.dependsOn generateGrammarSource

application {
    mainClass = 'org.javaculator.Cli'
}

jar {
    manifest {
        attributes(
                'Main-Class': 'org.javaculator.Cli'
        )
    }

    // This part is needed if you want to include dependencies in your JAR
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

